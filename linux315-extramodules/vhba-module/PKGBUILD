# Based on the file created for Arch Linux by:
# Ray Rashif <schiv@archlinux.org>
# Mateusz Herych <heniekk@gmail.com>
# Charles Lindsay <charles@chaoslizard.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux315-vhba-module
_pkgname=vhba-module
pkgver=20130607
_extramodules=extramodules-3.15-MANJARO
pkgrel=1
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux315')
makedepends=('linux315-headers')
provides=("$_pkgname=$pkgver")
groups=('linux315-extramodules')
install=$_pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        '60-vhba.rules'
        '0001-VHBA-module-define-scmd_dbg-and-scmd_warn-macros-onl.patch')
sha256sums=('12cfd1d9ab1c7421ef50e2bedc49f90f58afba6164d1acce14b6a4e6a68cc6a6'
         '8172d71045f5279f29eeb0224cc5ba85d14ff8514caa7718329cffe4f3574a31'
         '6ec6e89ab8076b16e2107eeb597f4020194526d1540eb06ebd43de3bd18e7262')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Fix build with Linux 3.15
  # http://sourceforge.net/p/cdemu/bugs/76/
  patch -Np2 -i $srcdir/0001-VHBA-module-define-scmd_dbg-and-scmd_warn-macros-onl.patch
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"

  make -j1 KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"

  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" \
    "$startdir/vhba-module.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/usr/lib/udev/rules.d/60-linux315-vhba.rules"
}

# vim:set ts=2 sw=2 et:
